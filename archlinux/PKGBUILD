# Maintainer: Kohei Suzuki <eagletmt@gmail.com>
pkgname=dvb-recpt1-git
pkgver=r16.d807fee
pkgrel=1
pkgdesc="recpt1 for earth-pt1 driver"
arch=(i686 x86_64)
url="https://github.com/eagletmt/dvb-recpt1"
license=('MIT')
depends=('boost-libs')
makedepends=('git' 'boost' 'cmake')
backup=('etc/recpt1/adapters.conf' 'etc/recpt1/channels.conf')
source=("$pkgname"::'git+https://github.com/eagletmt/dvb-recpt1')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  cmake . -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
sha1sums=('SKIP')
